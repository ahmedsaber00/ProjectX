apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'

//load app.properties
def appPropertiesFile = rootProject.file("app.properties")
def appPropertiesObject = new Properties()
//load all properties into appPropertiesObject
appPropertiesObject.load(new FileInputStream(appPropertiesFile))

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.afaqy.ptt"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        testInstrumentationRunner "com.afaqy.ptt.presentation.test.TestRunner"


        setProperty("archivesBaseName",
                "ProjectX_" + new Date().format("hhmm-dd-MM-yy")
                        + "_vn" + versionName + "_vc" + versionCode)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = "1.8" }
    dexOptions { javaMaxHeapSize "3g" }
    lintOptions { abortOnError false }

    signingConfigs {
        release {
            storeFile file(appPropertiesObject['storeFile'])
            storePassword appPropertiesObject['storePassword']
            keyAlias appPropertiesObject['keyAlias']
            keyPassword appPropertiesObject['keyPassword']
        }
    }

    flavorDimensions "default"
    productFlavors {
        production {
            applicationId "com.afaqy.ptt"
            dimension "default"
            versionNameSuffix "-production"
            versionCode rootProject.ext.app_version_code
            versionName "1.0.0"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${production_api_base_url}\"")
        }
        dev {
            applicationId "com.afaqy.ptt"
            dimension "default"
            versionNameSuffix "-dev"
            versionCode rootProject.ext.app_version_code
            versionName "1.0.0"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${dev_api_base_url}\"")
        }
        qc {
            applicationId "com.afaqy.ptt"
            dimension "default"
            versionNameSuffix "-test"
            versionCode rootProject.ext.app_version_code
            versionName "1.0.1"
            // ConfigFields
            buildConfigField("String", "API_BASE_URL", "\"${quality_api_base_url}\"")
        }
    }

    buildTypes {
        debug {
            if (applicationIdSuffix == "") {
                applicationIdSuffix ".dev"
            }
            //to speed up build disable Crashlytics plugin for debug builds
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/pttcodec.jar')

    implementation project(':Domain')
    implementation project(':Data')
    implementation project(':Remote')
    implementation project(':Cache')

    implementation presentationDependencies.javaxAnnotation
    implementation presentationDependencies.javaxInject
    implementation presentationDependencies.rxKotlin
    implementation presentationDependencies.kotlin
    implementation presentationDependencies.rxAndroid
    implementation presentationDependencies.androidxCoreKtx
    implementation presentationDependencies.androidxAnnotations
    implementation presentationDependencies.lifecycleRuntime
    implementation presentationDependencies.lifecycleExtensions
    implementation presentationDependencies.roomRuntime
    implementation presentationDependencies.roomRxJava
    implementation presentationDependencies.legacyV4
    implementation presentationDependencies.legacyV13
    implementation presentationDependencies.appCompat
    implementation presentationDependencies.recyclerView
    implementation presentationDependencies.constraintlayout
    implementation presentationDependencies.material
    implementation presentationDependencies.timber
    implementation presentationDependencies.glide
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt presentationDependencies.lifecycleCompiler
    kapt presentationDependencies.roomCompiler
    implementation remoteDependencies.okHttp
    implementation remoteDependencies.okHttpLogger

    //dagger
    implementation presentationDependencies.dagger
    kapt presentationDependencies.daggerCompiler

    implementation presentationDependencies.daggerAndroid
    implementation presentationDependencies.daggerAndroidSupport
    kapt presentationDependencies.daggerAndroidProcessor

    testImplementation presentationTestDependencies.junit
    testImplementation presentationTestDependencies.kotlinJUnit
    testImplementation presentationTestDependencies.assertj
    testImplementation presentationTestDependencies.mockito
    testImplementation presentationTestDependencies.mockitoAndroid
    testImplementation presentationTestDependencies.robolectric
    testImplementation presentationTestDependencies.archTesting
    kaptTest presentationDependencies.daggerCompiler

    // Instrumentation test dependencies
    androidTestImplementation presentationTestDependencies.junit
    androidTestImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.mockitoAndroid
    androidTestImplementation presentationTestDependencies.espressoCore
    androidTestImplementation presentationTestDependencies.androidxRunner
    androidTestImplementation presentationTestDependencies.androidxRules
    androidTestImplementation presentationTestDependencies.espressoIntents
    androidTestImplementation presentationTestDependencies.espressoContrib
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'

    //country picker
    implementation 'com.hbb20:ccp:2.3.7'

    //verification code
    implementation 'com.github.mukeshsolanki:android-otpview-pinview:2.1.0'
    implementation files('libs\\pttcodec.jar')

    implementation remoteDependencies.retrofit
    implementation remoteDependencies.retrofitConverter
    implementation remoteDependencies.retrofitAdapter
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
}
